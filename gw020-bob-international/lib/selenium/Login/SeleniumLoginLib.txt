*** Settings ***

Library                                       SeleniumLibrary   run_on_failure=SL.Capture Page Screenshot    WITH NAME    SL
Resource                                      ../../variables/AllVariables.txt

*** Variables ***
#${login_url}                                  192.168.1.1/main.html

*** Keywords ***
###Login###

Open Browser To Login Page
    [Arguments]                               ${remote_selenium_server_tmp}
    ...                                       ${url}
    ...                                       ${browser}=firefox
    SL.Open Browser                           ${url}
    ...                                       ${browser}
    ...                                       None
    ...                                       http://${remote_selenium_server_tmp}:5555/wd/hub
    SL.Wait Until Element Is Visible          xpath=//button[@class="loginBtn"]
    ...                                       timeout=20s
    ...                                       error=\nLogin Page Does Not Found!

Input Username
    [Arguments]                               ${username}
    SL.Wait Until Element Is Visible          xpath=//input[@id="userName"]
    SL.Input Text                             xpath=//input[@id="userName"]      ${username}

Input User Password
    [Arguments]                               ${password}
    SL.Wait Until Element Is Visible          xpath=//input[@id="pcPassword"]
    SL.Input Password                         xpath=//input[@id="pcPassword"]    ${password}

Submit Credentials
    SL.Wait Until Element Is Visible          xpath=//button[@class="loginBtn"]
    SL.Click Button                           xpath=//button[@class="loginBtn"]

###Verify###

Welcome Page Should Be Open
    SL.Wait Until Element Is Visible          xpath=//frame[@name="basefrm"]    
    SL.Select Frame                           xpath=//frame[@name="basefrm"]
    SL.Wait Until Page Contains               Device Info        timeout=20s     error=\nWelcome Page Does Not Found!
    SL.Page Should Contain                    Device Info
    SL.Unselect Frame


Login To Browser
    [Arguments]                               ${remote_selenium_server_tmp}
    ...                                       ${url}
    ...                                       ${username}
    ...                                       ${password}
    ...                                       ${browser}
    Open Browser To Login Page                remote_selenium_server_tmp=${remote_selenium_server_tmp}
    ...                                       url=${url}
    ...                                       browser=${browser}
    Input Username                            username=${username}
    Input User Password                       password=${password}
    Submit Credentials

Change Password
    [Arguments]                               ${inUserName}   ${old_pass}   ${new_pass}   ${cfm_new_pass}
    SL.Input Text                             xpath=//input[@name='inUserName']         ${inUserName}
    SL.Input Text                             xpath=//input[@name='inOrgPassword']      ${old_pass}
    SL.Input Text                             xpath=//input[@name='inPassword']         ${new_pass}
    SL.Input Text                             xpath=//input[@name='pwdCfm']             ${cfm_new_pass}
    SL.Click Button                           xpath=//input[@value='Apply/Save']
    SL.Wait Until Page Contains               Message
    ...                                       timeout=20s
    ...                                       error=\nMessage:Password change successful Does Not Found
    SL.Click Button                           xpath=//input[@type='button']

Change Wrong Password
    [Arguments]             ${inUserName}=admin        ${old_pass}=admin      ${new_pass}=admin     ${cfm_new_pass}=admin
    SL.Wait Until Element Is Visible       xpath=//input[@name="inUserName"]     timeout=10s              error=\nUsername Textbox Does Not Show Up!
    SL.Input Text              xpath=//input[@name="inUserName"]                 ${inUserName}
    SL.Wait Until Element Is Visible       xpath=//input[@name="inOrgPassword"]       timeout=10s              error=\nOld Password Textbox Does Not Show Up!
    SL.Input Password          xpath=//input[@name="inOrgPassword"]                   ${old_pass}
    SL.Wait Until Element Is Visible       xpath=//input[@name="inPassword"]       timeout=10s              error=\nNew Password Textbox Does Not Show Up!
    SL.Input Password          xpath=//input[@name="inPassword"]                   ${new_pass}
    SL.Wait Until Element Is Visible       xpath=//input[@name="pwdCfm"]       timeout=10s              error=\nConfirm New Password Textbox Does Not Show Up!
    SL.Input Password          xpath=//input[@name="pwdCfm"]                   ${cfm_new_pass}
    SL.Wait Until Element Is Visible       xpath=//input[@value="Apply/Save"]  timeout=10s              error=\nApply Save Button Does Not Show Up!
    SL.Click Button            xpath=//input[@value="Apply/Save"] 
    SL.Handle Alert

Login To Admin User With Wrong Password
    [Arguments]                               ${remote_selenium_server_tmp}
    ...                                       ${url}
    ...                                       ${username}
    ...                                       ${password_1}
    ...                                       ${password_2}
    ...                                       ${password_3}
    ...                                       ${browser}
    #The first time
    Log To Console                            Input Wrong Password the first time
    Open Browser To Login Page                remote_selenium_server_tmp=${remote_selenium_server_tmp}
    ...                                       url=${url}
    ...                                       browser=${browser}
    SL.Wait Until Element Is Visible          xpath=//button[@class="loginBtn"]
    ...                                       timeout=20s
    ...                                       error=\nLogin Page Does Not Found!
    Input Username                            username=${username}
    Input User Password                       password=${password_1}
    Submit Credentials
    SL.Page Should Contain                    The username or password is incorrect

    #The second time
    Log To Console                            Input Wrong Password the second time
    SL.Wait Until Element Is Visible          xpath=//button[@class="loginBtn"]
    ...                                       timeout=20s
    ...                                       error=\nLogin Page Does Not Found!
    Input Username                            username=${username}
    Input User Password                       password=${password_2}
    Submit Credentials
    SL.Page Should Contain                    The username or password is incorrect

    #The third time
    Log To Console                            Input Wrong Password the third time
    SL.Wait Until Element Is Visible          xpath=//button[@class="loginBtn"]
    ...                                       timeout=20s
    ...                                       error=\nLogin Page Does Not Found!
    Input Username                            username=${username}
    Input User Password                       password=${password_3}
    Submit Credentials
    SL.Page Should Contain                    The username or password is incorrect
    SL.Element Should Be Disabled             xpath=//input[@id="userName"]
    SL.Element Should Be Disabled             xpath=//input[@id="pcPassword"]
    Sleep                                     180
    SL.Element Should Be Enabled              xpath=//input[@id="userName"]
    SL.Element Should Be Enabled              xpath=//input[@id="pcPassword"]

Login with correct password
    Log To Console                            \nLogin to WEBGUI with correct password
    Login To Browser                          remote_selenium_server_tmp=${REMOTE_SELENIUM_SERVER}
    ...                                       url=${LOGIN_URL}
    ...                                       username=${ONT_WEB_USER}
    ...                                       password=${ONT_WEB_PASS}
    ...                                       browser=${BROWSER}
    Welcome Page Should Be Open


Logout From Admin User
    [Arguments]                               ${REMOTE_SELENIUM_SERVER}
    ...                                       ${LOGIN_URL}
    ...                                       ${ONT_WEB_USER}
    ...                                       ${ONT_WEB_PASS}
    ...                                       ${BROWSER}

    Login To Browser                          remote_selenium_server_tmp=${REMOTE_SELENIUM_SERVER}
    ...                                       url=${LOGIN_URL}
    ...                                       username=${ONT_WEB_USER}
    ...                                       password=${ONT_WEB_PASS}
    ...                                       browser=${BROWSER}
    Welcome Page Should Be Open
    SL.Select Frame                           xpath=//frame[@name="menufrm"]
    SL.Click Element                          xpath=//a[@id="79"]
    SL.Handle Alert
    SL.Unselect Frame
    SL.Close All Browsers

Go To Access Control Page
    [Arguments]                               ${url}=http://192.168.1.1/password.html 
    SL.Go To                                  ${url}
    SL.Wait Until Page Contains               Access Control -- Passwords          timeout=10s       error=\nChange Password Page Does Not Show Up!
    SL.Page Should Contain                    Access Control -- Passwords

Logout Page
    [Documentation]                           logout_url=http://lan_ip_address or logout_url=http://wan_ip_address
    ...                                       user=admin or user=operator
    [Arguments]                               ${logout_url}=http://192.168.1.1    ${user}=admin
    SL.Go To                                  ${logout_url}
    Welcome Page Should Be Open
    SL.Select Frame                           xpath=//frame[@name="menufrm"]
    Run Keyword If                            '${user}' == 'admin'        SL.Click Element    xpath=//a[@id="76"]
    ...    ELSE IF                            '${user}' == 'operator'     SL.Click Element    xpath=//a[@id="81"]
    SL.Handle Alert
    SL.Unselect Frame
    SL.Wait Until Element Is Visible          xpath=//button[@class="loginBtn"]
    ...                                       timeout=20s
    ...                                       error=\nLogin Page Does Not Found!

Login Retry With Another Password
    [Arguments]                             ${username}
    ...                                     ${new_password}
    ...                                     ${default_password}

    Input Username                          ${username}
    Input User Password                     ${new_password}
    Submit Credentials
    ${password_incorrect}=                  Run Keyword And Return Status    SL.Page Should Contain    The username or password is incorrect
    Run Keyword If                          ${password_incorrect} == True        Change Password And Relogin    username=${username}    default_password=${default_password}    new_pass=${new_password}
    ...    ELSE                             Welcome Page Should Be Open 

Change Password And Relogin
    [Arguments]                             ${username}
    ...                                     ${default_password}
    ...                                     ${new_pass}

    Input Username                          ${username}
    Input User Password                     ${default_password}
    Submit Credentials
    SL.Wait Until Page Contains             Access Control -- Passwords          timeout=10s       error=\nChange Password Page Does Not Show Up!
    SL.Page Should Contain                  Access Control -- Passwords
    ###Change Password
    Log To Console                          \nChange password from Operator
    Change Password                         inUserName=${username}
    ...                                     old_pass=${default_password}
    ...                                     new_pass=${new_pass}
    ...                                     cfm_new_pass=${new_pass}

    SL.Wait Until Element Is Visible        xpath=//button[@class="loginBtn"]
    ...                                     timeout=20s
    ...                                     error=\nLogin Page Does Not Found!
    Input Username                          ${username}
    Input User Password                     ${new_pass}
    Submit Credentials
    Welcome Page Should Be Open

Logout And Login As Another User
    [Arguments]                             ${ip_address_logout}
    ...                                     ${ip_address_login}
    ...                                     ${user_logout}
    ...                                     ${user_login}
    ...                                     ${default_password}
    ...                                     ${new_password}

    SL.Go To                                url=http://${ip_address_logout}
    Sleep                                   2s
    ${login_button_visible}=                Run Keyword And Return Status    SL.Element Should Be Visible    xpath=//button[@class="loginBtn"]

    Run Keyword If                          ${login_button_visible} == True    Login Retry With Another Password    ${user_logout}    ${new_password}    ${default_password}
    ...    ELSE                             Welcome Page Should Be Open    

    Logout Page                             logout_url=http://${ip_address_logout}    user=${user_logout}
    Sleep                                   2s
    SL.Go To                                url=http://${ip_address_login}
    SL.Wait Until Element Is Visible        xpath=//button[@class="loginBtn"]
    ...                                     timeout=20s
    ...                                     error=\nLogin Page Does Not Found!
    Input Username                          ${user_login}
    Input User Password                     ${new_password}
    Submit Credentials

